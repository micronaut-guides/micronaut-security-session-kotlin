buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE"
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.15"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        //tag::buildscriptDependenciesWebdriverBinaries[]
        classpath "gradle.plugin.com.energizedwork.webdriver-binaries:webdriver-binaries-gradle-plugin:1.4"
        //end::buildscriptDependenciesWebdriverBinaries[]
    }
}

version "0.1"
group "example.micronaut"

apply plugin:"io.spring.dependency-management"
apply plugin:"com.github.johnrengelman.shadow"
apply plugin:"application"
//tag::applygroovy[]
apply plugin:"groovy"
//end::applygroovy[]
apply plugin:"net.ltgt.apt-eclipse"
apply plugin:"net.ltgt.apt-idea"
apply plugin:"kotlin"
apply plugin:"kotlin-kapt"

//tag::applyPluginWebdriverBinaries[]
apply plugin:"com.energizedwork.webdriver-binaries"
//end::applyPluginWebdriverBinaries[]

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencyManagement {
    imports {
        mavenBom "io.micronaut:bom:$micronautVersion"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "io.micronaut:runtime"
    compile "io.micronaut:http-client"
    compile "io.micronaut:http-server-netty"
    kapt "io.micronaut:inject-java"
    kaptTest "io.micronaut:inject-java"
    runtime "ch.qos.logback:logback-classic:1.2.3"
    runtime "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4.1"

    //tag::securitydependency[]
    compile "io.micronaut:views"
    compile "io.micronaut:security-session"
    //end::securitydependency[]

    //tag::velocity[]
    compile "org.apache.velocity:velocity-engine-core:2.0"
    //end::velocity[]

    //tag::spock[]
    testCompile "org.codehaus.groovy:groovy-all:2.5.2"
    testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
    //end::spock[]

    //tag::geb[]
    testCompile "org.gebish:geb-spock:2.1"
    testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:3.6.0"
    testRuntime "org.seleniumhq.selenium:selenium-firefox-driver:3.6.0"
    testCompile "org.seleniumhq.selenium:selenium-support:3.6.0"
    testCompile "org.seleniumhq.selenium:selenium-api:3.6.0"
    //end::geb[]

}

shadowJar {
    mergeServiceFiles()
}

mainClassName = "example.micronaut.Application"

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        //Will retain parameter names for Java reflection
        javaParameters = true
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        javaParameters = true
    }
}

//tag::webdriverBinaries[]
webdriverBinaries {
    chromedriver '2.41'
    geckodriver '0.21.0'
}
//end::webdriverBinaries[]

//tag::taskWithTypeTest[]
tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}
//end::taskWithTypeTest[]
